$ ->
  $.ajax '/tooltips/enabled_selectors', success: (data) -> # uses request origin to restrict returned items
    tooltipsToInsert = data
    (new AntCat.SelectorTooltips).createTooltips tooltipsToInsert
    (new AntCat.Tooltipify).tooltipifyAll()

$ ->
  $.ajax '/tooltips/render_missing_tooltips', success: (data) ->
    if data.show_missing_tooltips == true
      $('label, button, .ui-button, .apply_tooltip').not('.display_button, .remove_tooltip').each (index, element) =>
        $(element).after("""\
          <a class = "create_tooltip" > \
          <img class="help_icon tooltip " \
           title="Create or edit tooltip" src='<%= image_path 'create_tip.png' %>' alt="Help" /></a>\
           """)
        $(element).next().click  ->
          if $(this).next().attr('href') == undefined
            selector = encodeURIComponent(OptimalSelect.select(element));

            $(this).attr('href',"/tooltips/new/?selector=" +
                selector +
                "&referral=" +
                encodeURIComponent(window.location.href)
            )
          else
            $(this).attr('href',$(this).next().attr('href'))

class AntCat.SelectorTooltips
  SELECTOR_TOOLTIP_CLASS = 'selector-tooltip'
  SELECTOR_TEST_TOOLTIP_CLASS = 'selector-test-tooltip'

  # Accepts an array of arrays in this format: [['jquery_selector', 'tooltip_text', 'id']]
  createTooltips: (tooltips) ->
    for tooltip in tooltips
      selector = tooltip[0]
      title = tooltip[1]
      id = tooltip[2]
      @createTooltip selector, title, id

  createTooltip: (selector, title, id, test_tooltip = false) =>
    iconElement = $(@_createIcon title, id)
    iconElement.addClass(SELECTOR_TEST_TOOLTIP_CLASS) if test_tooltip

    iconElement.insertAfter $(selector)

  removeAllSelectorTestTooltips: -> $(".#{SELECTOR_TEST_TOOLTIP_CLASS}").remove()

  _createIcon: (title, id) => # TODO move the link from this function
    """<a class="#{SELECTOR_TOOLTIP_CLASS}" href="/tooltips/#{id}""" +
      "?referral=" +
      encodeURIComponent(window.location.href) +
    """">\
       <img class="help_icon tooltip #{SELECTOR_TOOLTIP_CLASS}" \
       title="#{title}" src='<%= image_path 'help.png' %>' alt="Help" /></a>"""

class AntCat.Tooltipify
  tooltipifyAll: ->
    $('.tooltip').tooltip
      show: false
      content: -> $(this).prop('title') # Without this, HTML is displayed as text.
      position:
        my: "left top"
        at: "right bottom"
      close: (event, ui) -> # Keep open if the tooltip is hovered.
        startHoverHandler = -> $(this).stop(true)
        stopHoverHandler = -> $(this).remove()
        ui.tooltip.hover startHoverHandler, stopHoverHandler
